apply plugin: 'maven-publish'

task androidJavadoc(type: Javadoc) {
    if (plugins.hasPlugin('android-library')) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        android.libraryVariants.all { variant ->
            if (variant.name == 'release') {
                owner.classpath += variant.javaCompileProvider.get().classpath
            }
        }
    }
    else {
        source = sourceSets.main.allJava
        classpath += configurations.runtimeClasspath
        classpath += configurations.compileClasspath
    }
    exclude '**/R.html', '**/R.*.html', '**/index.html', '**/*.kt'
    options.encoding 'utf-8'
    options {
        addStringOption 'docencoding', 'utf-8'
        addStringOption 'charset', 'utf-8'
        addStringOption 'source', '8'
        addStringOption('Xdoclint:none', 'quiet')
        links 'https://docs.oracle.com/javase/7/docs/api/'
        links 'https://d.android.com/reference'
        links 'https://developer.android.com/reference/androidx/'
    }
}

task androidJavadocJar(type: Jar, dependsOn: androidJavadoc) {
    archiveClassifier.set('javadoc')
    from androidJavadoc.destinationDir
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

task javaSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (plugins.hasPlugin('android-library')) {
        from android.sourceSets.main.java.srcDirs
    }
    else {
        from sourceSets.main.allSource
    }
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/improve-ai/android-sdk")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            // Creates a Maven publication called 'library'.
            release(MavenPublication) {
                if (plugins.hasPlugin('android-library')) {
                    from components.release
                }
                else if (plugins.hasPlugin('java-library')) {
                    from components.java
                    jar.preserveFileTimestamps = false
                    jar.reproducibleFileOrder = true
                }

                artifact javaSourcesJar

                groupId rootProject.ext.pomGroupID
                version rootProject.ext.sdkVersion
                pom {
                    name = artifactId
                    description = 'ImproveAI SDK'
                    url = 'https://github.com/improve-ai/android-sdk'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/improve-ai/android-sdk.git'
                        url = 'https://github.com/improve-ai/android-sdk'
                    }
                    developers {
                        developer {
                            id = 'improve-ai'
                            name = 'Justin Chapweske'
                            email = 'justin@improve.ai'
                        }
                    }
                }
            }
        }
        // No repositories are configured here, since"gradle-nexus.publish-plugin" will create its
        // own using the configuration in the project's gradle file
    }
}

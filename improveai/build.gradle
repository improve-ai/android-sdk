plugins {
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    api 'ai.h2o:xgboost-predictor:0.3.17'
    implementation 'com.google.code.gson:gson:2.8.7'
//    implementation 'junit:junit:4.12'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

// Generate a BuildProperties Java class
task generateJava {
    ext.outputDir = "$buildDir/generated/java"
    doLast {
        mkdir "$outputDir/ai/improve/constants"
        file("$outputDir/ai/improve/constants/BuildProperties.java").text =
                """|package ai.improve.constants;
               |public class BuildProperties {
               |    public static String getSDKVersion() { return "${rootProject.ext.sdkVersion}"; }
               |}""".stripMargin()
    }
}
compileJava.dependsOn generateJava
sourceSets.main.java.srcDir generateJava.outputDir

javadoc {
    source = sourceSets.main.allJava
    classpath += configurations.compile
    exclude '**/R.html', '**/R.*.html', '**/index.html', '**/*.kt',
            '**/constants/*',
            'ai/improve/encoder',
            'ai/improve/ksuid',
            'ai/improve/util',
            'ai/improve/xgbpredictor',
            'ai/improve/provider/PersistenceProvider*'
}

apply from: '../publish.gradle'